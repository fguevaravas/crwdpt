{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Numerical verification of some discrete boundary potential formulas\n",
    "This notebook is supplementary material for \n",
    "\n",
    "\"_A potential theory on weighted graphs_\" by\n",
    "Fernando Guevara Vasquez and Trent DeGiovanni\n",
    "\n",
    "The idea here is to generate a random graph subdivided into boundary nodes $B$, exterior nodes $\\Omega^+$ and a region of interest $\\Omega^-$ that is connected to the rest of the nodes via $\\partial\\Omega$. Then we test the different identities on appropriate solutions to the Dirichlet problem on a graph. All the tests here work by computing the difference between the left and right hand side of a particular equality and checking that we numerically a vector close to zero using the `@test` macro. If all the tests in a cell pass, then `Test Passed` is displayed in the cell output.\n",
    "\n",
    "In this code we use `Î©e` for exterior and `Î©i` for interior (and similarly for the other quantities that depend on interior vs exterior)}\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Graph construction"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "The sets are:\n",
      "Î©i  = 1:10\n",
      "âˆ‚Î© = 11:15\n",
      "Î©e  = 16:25\n",
      "B  = 26:30\n",
      "V  = 1:30\n",
      "Váµ’ = [16, 17, 18, 19, 20, 21, 22, 23, 24, 25, 11, 12, 13, 14, 15, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10]\n"
     ]
    }
   ],
   "source": [
    "using LinearAlgebra, SparseArrays, Test\n",
    "\n",
    "nB = 5; nÎ©e = 10; nâˆ‚Î© = 5; nÎ©i = 10 # select number of nodes\n",
    "\n",
    "nV  = nB + nÎ©e + nâˆ‚Î© + nÎ©i\n",
    "# define sets of nodes as indices\n",
    "Î©i = 1:nÎ©i\n",
    "âˆ‚Î© = nÎ©i .+ (1:nâˆ‚Î©)\n",
    "Î©e = (nÎ©i + nâˆ‚Î©) .+ (1:nÎ©e)\n",
    "B = (nÎ©i + nâˆ‚Î© + nÎ©e) .+ (1:nB)\n",
    "V = 1:nV # all vertices\n",
    "Váµ’ = Î©e âˆª âˆ‚Î© âˆª Î©i # all the vertices minus the ones boundary conditions are imposed\n",
    "println(\"\"\"The sets are:\n",
    "    Î©i  = $Î©i\n",
    "    âˆ‚Î© = $âˆ‚Î©\n",
    "    Î©e  = $Î©e\n",
    "    B  = $B\n",
    "    V  = $V\n",
    "    Váµ’ = $Váµ’\"\"\")\n",
    "\n",
    "# generate incidence matrix with conductivities that are uniform random (0,1)\n",
    "# using ErdÃ¶s-Renyi model\n",
    "p = 0.5 # probability of getting a link between two nodes\n",
    "A = spzeros(nV,nV)\n",
    "for i=1:nV, j=1:i-1\n",
    "    A[i,j] =  rand()*(rand()>p)\n",
    "end\n",
    "A = -(A + A')/2\n",
    "\n",
    "# get rid of connections between Î©e and Î©i\n",
    "A[Î©e,Î©i] .= 0; A[Î©i,Î©e] .= 0\n",
    "\n",
    "# get rid of connections between B and anything other than Î©e\n",
    "A[B,âˆ‚Î© âˆª Î©i] .= 0; A[âˆ‚Î© âˆª Î©i,B] .= 0\n",
    "\n",
    "# check âˆ‚Î© is connected to both Î©i and Î©e\n",
    "@assert sum(A[âˆ‚Î©,Î©i])<0 && sum(A[âˆ‚Î©,Î©e])<0 # if error is raised, simply rerun cell to draw another graph"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Laplacian and Green functions\n",
    "Note the identities that we find are not affected by the sign of the Laplacian (i.e. whether we choose it to be a positive semidefinite or negative semidefinite matrix)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "# ensures a matrix has zero row sum by adjusting diagonal elements\n",
    "zerosum(A) = A - spdiagm(A*ones(size(A,1))) \n",
    "L = zerosum(A)  # negative semi-definite Laplacian\n",
    "#L = -zerosum(A) # positive semi-definite graph Laplacian\n",
    "\n",
    "# Green functions with 0 Dirichlet condition on B\n",
    "G = zeros(nV,nV)\n",
    "G[Váµ’,Váµ’] = inv(Matrix(L[Váµ’,Váµ’]));"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## The exterior and interior Laplacians\n",
    "The edge partition of the unity for conductivities we chose is of the form $p^-|_{\\mathcal{E}^{\\partial\\Omega}}=\\alpha$ and $p^+|_{\\mathcal{E}^{\\partial\\Omega}}=1-\\alpha$ for $\\alpha \\in [0,1]$. The exterior $L^+$ and interior $L^-$ Laplacians should satisfy $L=L^+ + L^-$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "Î± = 0.3 # blending factor (anything in [0,1])\n",
    "\n",
    "# exterior Laplacian (zero inside Î©)\n",
    "Le = copy(L);\n",
    "Le[Î©i,:] .= 0; Le[:,Î©i] .= 0\n",
    "Le[âˆ‚Î©,âˆ‚Î©] .*= 1-Î±\n",
    "Le = zerosum(Le);\n",
    "\n",
    "# interior Laplacian (zero outside Î©, i.e. BâˆªE)\n",
    "Li = copy(L);\n",
    "Li[BâˆªÎ©e,:] .= 0; Li[:,BâˆªÎ©e] .=0 ;\n",
    "Li[âˆ‚Î©,âˆ‚Î©] .*= Î±\n",
    "Li = zerosum(Li);\n",
    "@test norm(Li+Le-L) â‰ˆ 0 atol=1e-10"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Trace operators and single, double layer potential operators\n",
    "Here we define:\n",
    "* Dirichlet trace operator $\\gamma_0$ and the Neumann trace operators $\\gamma_1^\\pm$\n",
    "* Single $\\mathcal{S}$ and double layer $\\mathcal{D}^\\pm$ potential operators\n",
    "* Indicator function $\\mathbb{1}$ of a set"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "ðŸ™ (generic function with 1 method)"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "R = I(nV)[âˆ‚Î©,:] # restriction to âˆ‚Î©\n",
    "Î³0 = R          # Dirichlet trace operator\n",
    "Î³1e = - R*Le    # exterior Neumann trace operator\n",
    "Î³1i =   R*Li    # interior Neumann trace operator\n",
    "\n",
    "S  = G*Î³0'      # single layer potential operator\n",
    "De = G*Î³1e'     # exterior double layer potential operator\n",
    "Di = G*Î³1i'     # interior double layer potential operator\n",
    "\n",
    "com(C,D) = C*D - D*C        # matrix commutator\n",
    "\n",
    "ðŸ™(X) = [ i âˆˆ X for i âˆˆ V];  # indicator function of the set X"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Interior reproduction formulas"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# generate a u that solves the Dirichlet problem inside âˆ‚Î© âˆª Î©i\n",
    "u = zeros(nV)\n",
    "u[B] = randn(nB)\n",
    "u[Váµ’] = -L[Váµ’,Váµ’]\\(L[Váµ’,B]*u[B])\n",
    "\n",
    "# check interior reproduction formulas:\n",
    "@test norm(S*Î³1e*u - De*Î³0*u - u.*ðŸ™(âˆ‚Î© âˆª Î©i)) â‰ˆ 0 atol=1e-10\n",
    "@test norm(S*Î³1i*u - Di*Î³0*u - u.*ðŸ™(Î©i))      â‰ˆ 0 atol=1e-10\n",
    "\n",
    "# check formulas with commutators:\n",
    "@test norm(( G*com(Le,R'*R)*u - u)[âˆ‚Î© âˆª Î©i]) â‰ˆ 0 atol=1e-10\n",
    "@test norm((-G*com(Li,R'*R)*u - u)[Î©i])      â‰ˆ 0 atol=1e-10"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Continuity of flux for a harmonic function"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
      ]
     },
     "execution_count": 7,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "@test norm((Î³1e - Î³1i)*u) â‰ˆ 0 atol=1e-10"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Exterior reproduction formulas"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
      ]
     },
     "execution_count": 8,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# generate a v that solves teh Dirichlet problem outside  âˆ‚Î© âˆª Î©\n",
    "f = zeros(nV); f[Î©i] = randn(nÎ©i); # forcing term\n",
    "v = zeros(nV);\n",
    "v[Váµ’]=-L[Váµ’,Váµ’]\\f[Váµ’];\n",
    "\n",
    "# check exterior reproduction formulas:\n",
    "@test norm(-S*Î³1e*v + De*Î³0*v - v.*ðŸ™(Î©e))      â‰ˆ 0 atol=1e-10\n",
    "@test norm(-S*Î³1i*v + Di*Î³0*v - v.*ðŸ™(Î©eâˆªâˆ‚Î©))   â‰ˆ 0 atol=1e-10\n",
    "\n",
    "# check formulas with commutators:\n",
    "@test norm((-G*com(Le,R'*R)*v - v)[Î©e])        â‰ˆ 0 atol=1e-10\n",
    "@test norm(( G*com(Li,R'*R)*v - v)[Î©eâˆªâˆ‚Î©])     â‰ˆ 0 atol=1e-10"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Jump relations"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
      ]
     },
     "execution_count": 9,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "@test norm((Î³1e-Î³1i)*S  + I) â‰ˆ 0 atol=1e-10  # discontinuity of Neumann trace of single layer potential\n",
    "@test norm(Î³0*(De - Di) + I) â‰ˆ 0 atol=1e-10  # discontinuity of Dirichlet trace of double layer potential\n",
    "@test norm(Î³1e*Di - Î³1i*De)  â‰ˆ 0 atol=1e-10  # continuity of Neumann trace of double layer potential"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Boundary Layer Operators"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {},
   "outputs": [],
   "source": [
    "bS = Î³0*S          # Single Layer Operator\n",
    "bD = Î³0*(De+Di)/2   # Double Layer Operator\n",
    "bDa = (Î³1e+Î³1i)*S/2 # Adjoint Double Layer Operator\n",
    "bH = -Î³1e*Di;        # Hypersingular operator"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Jump relations for boundary layer operators"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
      ]
     },
     "execution_count": 11,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# discontinuity of Neumann trace of single layer potential\n",
    "@test norm(Î³1e*S - ( -I/2 + bD' )) â‰ˆ 0 atol=1e-10\n",
    "@test norm(Î³1i*S - (  I/2 + bD' )) â‰ˆ 0 atol=1e-10\n",
    "\n",
    "# discontinuity of Dirichlet trace of double layer potential\n",
    "@test norm(Î³0*De - ( -I/2 + bD  )) â‰ˆ 0 atol=1e-10\n",
    "@test norm(Î³0*Di - (  I/2 + bD  )) â‰ˆ 0 atol=1e-10"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## CalderÃ³n projectors"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
      ]
     },
     "execution_count": 12,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "C = [-bD     bS\n",
    "     bH     bD' ]\n",
    "Pi = I/2 + C # interior CalderÃ³n projector\n",
    "Pe = I/2 - C # exterior CalderÃ³n projector\n",
    "@test norm(Pi*Pi - Pi) â‰ˆ 0 atol=1e-10\n",
    "@test norm(Pe*Pe - Pe) â‰ˆ 0 atol=1e-10"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Identities between boundary layer operators\n",
    "\\begin{align}\n",
    " S H &= -D^2 + \\frac{I}{4},\\\\\n",
    " H S &= -(D')^2 + \\frac{I}{4},\\\\\n",
    " S D' &= D S,\\\\\n",
    " D' H &= H D.\n",
    "\\end{align}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
      ]
     },
     "execution_count": 13,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "@test norm(bS*bH - (-bD^2+I/4)) â‰ˆ 0 atol=1e-10\n",
    "@test norm(bH*bS - (-(bD')^2+I/4)) â‰ˆ 0 atol=1e-10\n",
    "@test norm(bS*bD' - bD*bS) â‰ˆ 0 atol=1e-10\n",
    "@test norm(bD'*bH - bH*bD) â‰ˆ 0 atol=1e-10"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## DtN map tests\n",
    "Checks DtN map formula $\\Lambda = (\\frac{I}{2} + D') S^{-1} = S^{-1} (\\frac{I}{2} + D) = H + (\\frac{I}{2} + D') S^{-1} (\\frac{I}{2} + D)$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 21,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
      ]
     },
     "execution_count": 21,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# interior\n",
    "DtN1 = Li[âˆ‚Î©,âˆ‚Î©] - Li[âˆ‚Î©,Î©i]*(Li[Î©i,Î©i]\\Array(Li[Î©i,âˆ‚Î©]))\n",
    "DtN2 = (I/2+bD')*inv(bS) \n",
    "DtN3 = inv(bS)*(I/2+bD)\n",
    "DtN4 = bH + (I/2+bD')*inv(bS)*(I/2+bD)\n",
    "@test norm(DtN1 - DtN2) â‰ˆ 0 atol=1e-10\n",
    "@test norm(DtN1 - DtN3) â‰ˆ 0 atol=1e-10\n",
    "@test norm(DtN1 - DtN4) â‰ˆ 0 atol=1e-10\n",
    "\n",
    "# exterior\n",
    "DtN1 = -(Le[âˆ‚Î©,âˆ‚Î©] - Le[âˆ‚Î©,Î©e]*(Le[Î©e,Î©e]\\Array(Le[Î©e,âˆ‚Î©])))\n",
    "DtN2 = (-I/2+bD')*inv(bS) \n",
    "DtN3 = inv(bS)*(-I/2+bD)\n",
    "DtN4 = -bH -(-I/2+bD')*inv(bS)*(-I/2+bD)\n",
    "@test norm(DtN1 - DtN2) â‰ˆ 0 atol=1e-10\n",
    "@test norm(DtN1 - DtN3) â‰ˆ 0 atol=1e-10\n",
    "@test norm(DtN1 - DtN4) â‰ˆ 0 atol=1e-10"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "5Ã—5 Matrix{Float64}:\n",
       " -0.916003    0.0668366   0.113667   0.359822    0.0755227\n",
       "  0.0668366  -1.57501     0.419837   0.0689125   0.0987656\n",
       "  0.113667    0.419837   -1.84274    0.348351    0.289487\n",
       "  0.359822    0.0689125   0.348351  -1.76443     0.454736\n",
       "  0.0755227   0.0987656   0.289487   0.454736   -1.59234"
      ]
     },
     "execution_count": 17,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "Dt1"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Check invertibility of operators\n",
    "These are based on the uniqueness of certain problems"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 148,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
      ]
     },
     "execution_count": 148,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "@test rank(bS) == nB # BSO should be invertible (uniqueness Dir prob)\n",
    "@test rank(-I/2 + bD) == nB # uniqueness exterior Neumann prob\n",
    "@test rank(I/2 + bD) == nB-1 # interior Neumann prob up to constants\n",
    "@test norm((I/2 + bD)*ones(nâˆ‚Î©)) â‰ˆ 0 atol=1e-10"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Spectrum of Neumann-PoincarÃ© operator inside [-1/2,1/2]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 149,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
      ]
     },
     "execution_count": 149,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "@test all(abs.(eigvals(bD')) .<= 1/2+1e-10)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
